// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    //// Replace this comment with your code.
    Mux16(a=x, b[0..15]=false, sel=zx, out=pzx);
    Not16(in=pzx, out=nox);
    Mux16(a=pzx, b=nox, sel=nx, out=pznx);
    Mux16(a=y, b[0..15]=false, sel=zy, out=pzy);
    Not16(in=pzy, out=noy);
    Mux16(a=pzy, b=noy, sel=ny, out=pzny);

    And16(a=pznx, b=pzny, out=and);
    Add16(a=pznx, b=pzny, out=add);
    Mux16(a=and, b=add, sel=f, out=pro);

    Not16(in=pro, out=npro);
    Mux16(a=pro, b=npro, sel=no, out[0..7]=outo1, out[8..14]=outo2, out[15]=sign);

    Or8Way(in=outo1, out=fore);
    Or8Way(in[0..6]=outo2, in[7]=sign, out=back);
    Or(a=fore, b=back, out=ors);
    Not(in=ors, out=zr);

    Or(a=sign, b=false, out=ng);

    Or16(a[0..7]=outo1, a[8..14]=outo2, a[15]=sign, b[0..15]=false, out=out);
}